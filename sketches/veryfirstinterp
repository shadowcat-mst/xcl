use strictures 2;
use XCL::Value;
use Try::Tiny;
use experimental qw(signatures);
use feature qw(say);

sub plus ($x, $y) {
  if ($x->type eq 'Int' and $y->type eq 'Int') {
    return $x->new_value($x->data + $y->data);
  }
  if ($x->type eq 'String' and $y->type eq 'String') {
    return $x->new_value($x->data.$y->data);
  }
  die "No handler for (${\join(',',map $_->type, @_)})\n";
}

sub eval_expr ($env, $expr) {
  if ($expr->type eq 'Symbol') {
    if (my $val = $env->{$expr->data}) {
      return $val;
    }
    die "No such name in scope: ${\$expr->data}\n";
  }
  return $expr;
}

sub wrap ($thing) {
  return sub ($env, @args) {
    $thing->(map eval_expr($env, $_), @args)
  };
}

sub Int ($v) { XCL::Value->new(type => 'Int', data => $v) }
sub String ($v) { XCL::Value->new(type => 'String', data => $v) }
sub Symbol ($v) { XCL::Value->new(type => 'Symbol', data => $v) }

my $plus = wrap \&plus;

say +($plus->({}, map Int($_), 1, 4)->data);

say +($plus->({}, map String($_), 'Hello ', 'World!')->data);

try { $plus->({}, Int(1), String(3)) } catch { print $_ };

my $env = { x => Int(3), y => Int(7), z => Int(10) };

say +($plus->($env, Symbol('x'), Symbol('z'))->data);
